#!ipxe

#  ipxe may fail fetching some files (especially wrt archlinux) from https
#  make sure to build ipxe with https and optionally with FTP as well
#  to do so, edit your ipxe's src/config/general.h and modify the line
#  DOWNLOAD_PROTO_HTTPS  from undef to define
#  do similarily for the line with FTP protocol if necessary
#

:selection

menu iPXE boot menu
item slax32         Boot Slax 32-bit
item slax64         Boot Slax 64-bit
item debian64		Boot debian 64bit installer
item debian		Boot debian 32bit installer
item debian64gui	Boot debian 64bit installer [gtk]
item debiangui		Boot debian 32bit installer [gtk]
item mageia64		Boot mageia 64bit installer
item mageia		Boot mageia 32bit installer
item opensuse64		Boot opensuse 64bit installer (current version)
item opensuse		Boot opensuse 32bit installer (current version)
item opensuset64	Boot opensuse 64bit installer (tumbleweed)
item opensuset		Boot opensuse 32bit installer (tumbleweed)
item opensusef64	Boot opensuse Factory 64bit installer
item opensusef32	Boot opensuse Factory 32bit installer
item opensuseleap	Boot opensuse Leap installer
item opensuset 	Boot opensuse Tumbleweed 32bit
item opensuset64 	Boot opensuse Tumbleweed 64bit
item plop		Boot plop boot manager
item tinycore		Boot tinycore linux
item tinycore64		Boot tinycore linux [64bit kernel only]
item tinycore64pure	Boot tinycore linux [fully 64bit version, pure64]
item dcore-wheezy	Boot dCore (debian wheezy version)
item dcore-jessie	Boot dCore (debian jessie version)
item dcore-stretch  Boot dCore (debian stretch)
item dcore-trusty	Boot dCore (ubuntu trusty version, 14.04)
item dcore-xenial   Boot dCore (ubuntu xenial version, 16.04)
item arch		Boot archlinux installer
item centos		Boot CentOS installer
item scientific		Boot Scientific Linux installer
item fedora		Boot Fedora installer
item fedora-mirror	Boot Fedora installer Server edition [mirror]
item fedora-mirror-workstation Boot Fedora installer Workstation edition [mirror]
item ubuntu		Boot Ubuntu installer
item ubuntu64		Boot Ubuntu installer [64bit]
item slitaz		Boot SliTaz
item slitaz-rolling	Boot SliTaz [rolling]
item alpine64     Boot Alpine Linux 64 [vanilla]
item alpine64virt     Boot Alpine Linux 64 [virt]
item alpine32     Boot Alpine Linux 32 [vanilla]
item alpine32virt     Boot Alpine Linux 32 [virt]
item localmenu      Boot extra local menu


item --key c config	Configure iPXE
item --key x exit	Exit iPXE
set menu-timeout 0

choose system || goto :selection

goto ${system}

:localmenu
dhcp
set localcredentials:string login:password@
set localweb:string 192.168.1.167
set localurl:string /pxeboot
set localwebserver:string http://${localcredentials:string}${localweb:string}${localurl:string}

chain ${localwebserver:string}/pxemenu

# there are more archs to use, adjust as necessary
:alpine64
dhcp
set arch:string x86_64
set type:string vanilla
set baseurl:string http://dl-cdn.alpinelinux.org/alpine/v3.8/
set imageurl:string http://dl-cdn.alpinelinux.org/alpine/v3.8/releases/${arch:string}/netboot-3.8.0/
initrd ${imageurl:string}/initramfs-${type:string}
kernel ${imageurl:string}/vmlinuz-${type:string}
boot vmlinuz-${type:string} alpine_repo=${baseurl:string}/main modloop=${baseurl:string}/releases/${arch:string}/netboot-3.8.0/modloop-${type:string}

:alpine64virt
dhcp
set arch:string x86_64
set type:string virt
set baseurl:string http://dl-cdn.alpinelinux.org/alpine/v3.8/
set imageurl:string http://dl-cdn.alpinelinux.org/alpine/v3.8/releases/${arch:string}/netboot-3.8.0/
initrd ${imageurl:string}/initramfs-${type:string}
kernel ${imageurl:string}/vmlinuz-${type:string}
boot vmlinuz-${type:string} alpine_repo=${baseurl:string}/main modloop=${baseurl:string}/releases/${arch:string}/netboot-3.8.0/modloop-${type:string}

:alpine32
dhcp
set arch:string x86
set type:string vanilla
set baseurl:string http://dl-cdn.alpinelinux.org/alpine/v3.8/
set imageurl:string http://dl-cdn.alpinelinux.org/alpine/v3.8/releases/${arch:string}/netboot-3.8.0/
initrd ${imageurl:string}/initramfs-${type:string}
kernel ${imageurl:string}/vmlinuz-${type:string}
boot vmlinuz-${type:string} alpine_repo=${baseurl:string}/main modloop=${baseurl:string}/releases/${arch:string}/netboot-3.8.0/modloop-${type:string}

:alpine32virt
dhcp
set arch:string x86
set type:string virt
set baseurl:string http://dl-cdn.alpinelinux.org/alpine/v3.8/
set imageurl:string ${baseurl:string}/releases/${arch:string}/netboot-3.8.0/
initrd ${imageurl:string}/initramfs-${type:string}
kernel ${imageurl:string}/vmlinuz-${type:string}
boot vmlinuz-${type:string} alpine_repo=${baseurl:string}/main modloop=${baseurl:string}/releases/${arch:string}/netboot-3.8.0/modloop-${type:string}

:slax32
dhcp
chain http://slax.org/ipxe/?arch=32bit

:slax64
dhcp
chain http://slax.org/ipxe/?arch=64bit

:scientific
  dhcp
  set baseurl:string http://ftp.scientificlinux.org/linux/scientific/7/x86_64/os/
  initrd ${baseurl:string}images/pxeboot/initrd.img
  chain ${baseurl:string}images/pxeboot/vmlinuz repo=${baseurl:string}


:slitaz
  dhcp
  set weburl http://mirror.slitaz.org/pxe/
  set 210:string ${weburl} && chain ${weburl}pxelinux.0 && boot ||
  set weburl http://mirror.switch.ch/ftp/mirror/slitaz/pxe/
  set 210:string ${weburl} && chain ${weburl}pxelinux.0 && boot ||
  set weburl http://download.tuxfamily.org/slitaz/pxe/
  set 210:string ${weburl} && chain ${weburl}pxelinux.0 && boot 

:slitaz-rolling
  dhcp
  sanboot http://mirror.slitaz.org/iso/rolling/slitaz-rolling.iso

:fedora


  dhcp
  set 209:string cfg/pxelinux.cfg
  set 210:string http://dl.fedoraproject.org/pub/alt/bfo/
  chain ${210:string}pxelinux.0

:fedora-mirror
# certain fedora mirrors picked by BFO entry (the one above)
# are missing .treeinfo file in the os/ subdir 
# this breaks installation
# if that happens to you, pick a specific mirror that works
# to work that issue around


#
# new layout, replace 'Workstation' with other directory names for fedora variants
#

  dhcp

  set baseurl:string http://ftp.icm.edu.pl/pub/Linux/fedora/linux/releases/28/Server/x86_64/os
  initrd ${baseurl:string}/images/pxeboot/initrd.img
  chain  ${baseurl:string}/images/pxeboot/vmlinuz repo=${baseurl:string}

:fedora-mirror-workstation
  dhcp

  set baseurl:string http://ftp.icm.edu.pl/pub/Linux/fedora/linux/releases/28/Workstation/x86_64/os
  initrd ${baseurl:string}/images/pxeboot/initrd.img
  chain  ${baseurl:string}/images/pxeboot/vmlinuz repo=${baseurl:string}


:centos

  dhcp
  set baseurl:string http://mirror.centos.org/centos/7/os/x86_64/
  initrd ${baseurl:string}isolinux/initrd.img
  chain  ${baseurl:string}isolinux/vmlinuz repo=${baseurl:string}


#
# vivid is currently broken, use syslinux menu with downloaded kernel and initrd instead for now
# changed it anyway, maybe it will get fixed
# https://bugs.launchpad.net/ubuntu/+source/syslinux/+bug/1385069
#


#
# as a workaround, load pxelinux from older ubuntu 
# and then load subsequent files from current release
# it will fail to load menu module, but if you press TAB
# you can get hints on available commands and kick off the install
# usually just typing in 'install' is all you need on that prompt
#

:ubuntu

  dhcp
  set 210:string http://archive.ubuntu.com/ubuntu/dists/bionic/main/installer-i386/current/images/netboot/ubuntu-installer/i386/
  initrd ${210:string}initrd.gz
  chain ${210:string}linux

:ubuntu64

  dhcp
  set 210:string http://archive.ubuntu.com/ubuntu/dists/bionic/main/installer-amd64/current/images/netboot/ubuntu-installer/amd64/
  initrd ${210:string}initrd.gz
  chain ${210:string}linux


#
# debian, replace links with your favorite mirror
#


:debian64

  dhcp
  set 210:string http://ftp.pl.debian.org/debian/dists/stable/main/installer-amd64/current/images/netboot/debian-installer/amd64/
  #chain ${210:string}pxelinux.0
  initrd ${210:string}initrd.gz
  chain ${210:string}linux

:debian

  dhcp
  set 210:string http://ftp.pl.debian.org/debian/dists/stable/main/installer-i386/current/images/netboot/debian-installer/i386/
#  chain ${210:string}pxelinux.0
  initrd ${210:string}initrd.gz
  chain ${210:string}linux

:debian64gui

  dhcp
  set 210:string http://ftp.pl.debian.org/debian/dists/stable/main/installer-amd64/current/images/netboot/gtk/debian-installer/amd64/
#  chain ${210:string}pxelinux.0
  initrd ${210:string}initrd.gz
  chain ${210:string}linux

:debiangui

  dhcp
  set 210:string http://ftp.pl.debian.org/debian/dists/stable/main/installer-i386/current/images/netboot/gtk/debian-installer/i386/
#  chain ${210:string}pxelinux.0
  initrd ${210:string}initrd.gz
  chain ${210:string}linux

:mageia64
  dhcp
  set baseurl:string http://ftp5.gwdg.de/pub/linux/mageia/distrib/cauldron/x86_64/isolinux/x86_64/

  initrd   ${baseurl:string}all.rdz
  imgfetch ${baseurl:string}vmlinuz
  boot vmlinuz || goto selection


:mageia
  dhcp

  set baseurl:string http://ftp5.gwdg.de/pub/linux/mageia/distrib/cauldron/i586/isolinux/i386/
  initrd   ${baseurl:string}all.rdz
  imgfetch ${baseurl:string}vmlinuz
  boot vmlinuz || goto selection


:opensuse64
  dhcp
#  set baseurl:string http://ftp.icm.edu.pl/pub/Linux/opensuse/distribution/openSUSE-current
  set baseurl:string http://download.opensuse.org/distribution/openSUSE-current
  set arch:string x86_64
  goto opensuse-generic-boot


:opensusef64

  dhcp
  set baseurl:string http://ftp.icm.edu.pl/pub/Linux/opensuse/factory
  set arch:string x86_64
  goto opensuse-generic-boot


:opensusef32
  dhcp
  set baseurl:string http://ftp.icm.edu.pl/pub/Linux/opensuse/factory
  set arch:string i386
  goto opensuse-generic-boot

:opensuseleap
  dhcp
  set baseurl:string http://ftp.icm.edu.pl/pub/Linux/opensuse/distribution/leap/42.2
  set arch:string x86_64
  goto opensuse-generic-boot

:opensuse
  dhcp
  set baseurl:string http://ftp.icm.edu.pl/pub/Linux/opensuse/distribution/openSUSE-current
  set arch:string i386
  goto opensuse-generic-boot

:opensuset
  dhcp
 # set baseurl:string http://download.opensuse.org/tumbleweed
  set baseurl:string http://ftp.icm.edu.pl/pub/Linux/opensuse/tumbleweed
  set arch:string i386
  goto opensuse-generic-boot

:opensuset64
  dhcp
  #set baseurl:string http://download.opensuse.org/tumbleweed
  set baseurl:string http://ftp.icm.edu.pl/pub/Linux/opensuse/tumbleweed
  set arch:string x86_64
  goto opensuse-generic-boot

:opensuse-generic-boot

  initrd   ${baseurl:string}/repo/oss/boot/${arch:string}/loader/initrd
  imgfetch ${baseurl:string}/repo/oss/boot/${arch:string}/loader/linux || goto selection
  boot     linux Textmode=1 install=${baseurl:string}/repo/oss 
#console=ttyAMA0 console=ttyS0 

:plop

  dhcp 
  imgfetch http://some_web_server/memdisk
  initrd http://some_web_server/plpbt.img
  chain memdisk || goto selection

# for some reason direct boot of plop doesn't seem to work correctly
# also, memdisk fails when the *.img file is packed with gzip 
# (which _really_ reduces size)


:tinycore
  dhcp 
  initrd http://repo.tinycorelinux.net/9.x/x86/release/distribution_files/core.gz
  initrd http://repo.tinycorelinux.net/9.x/x86/release/distribution_files/modules.gz
  imgfetch http://repo.tinycorelinux.net/9.x/x86/release/distribution_files/vmlinuz
  boot vmlinuz || goto selection

# all other tinycore args still apply here
# it's worth setting up a NFS share for your extensions + persistence in this mode
# to make full use of tinycore's features

# alternately you can load vmlinuz + rootfs.gz + modules.gz , for easier customization
# if you use your custom server


:tinycore64
  dhcp
  initrd http://repo.tinycorelinux.net/9.x/x86/release/distribution_files/core.gz
  initrd http://repo.tinycorelinux.net/9.x/x86/release/distribution_files/modules64.gz
  imgfetch http://repo.tinycorelinux.net/9.x/x86/release/distribution_files/vmlinuz64
  boot vmlinuz64 || goto selection


:tinycore64pure
  dhcp
  initrd http://repo.tinycorelinux.net/9.x/x86_64/release/distribution_files/corepure64.gz
  imgfetch http://repo.tinycorelinux.net/9.x/x86_64/release/distribution_files/vmlinuz64
  boot vmlinuz64 || goto selection

:dcore-wheezy

  dhcp
  initrd http://tinycorelinux.net/dCore/x86/release/dCore-wheezy/dCore-wheezy.gz
  imgfetch http://tinycorelinux.net/dCore/x86/release/dCore-wheezy/vmlinuz-wheezy
  boot vmlinuz-wheezy || goto selection

:dcore-stretch

  dhcp
  initrd http://tinycorelinux.net/dCore/x86/release/dCore-stretch/dCore-stretch.gz
  imgfetch http://tinycorelinux.net/dCore/x86/release/dCore-stretch/vmlinuz-stretch
  boot vmlinuz-stretch || goto selection

:dcore-jessie
  dhcp
  initrd http://tinycorelinux.net/dCore/x86/release/dCore-jessie/dCore-jessie.gz
  imgfetch http://tinycorelinux.net/dCore/x86/release/dCore-jessie/vmlinuz-jessie
  boot vmlinuz-jessie || goto selection

:dcore-trusty

  dhcp
  initrd http://tinycorelinux.net/dCore/x86/release/dCore-trusty/dCore-trusty.gz
  imgfetch http://tinycorelinux.net/dCore/x86/release/dCore-trusty/vmlinuz-trusty
  boot vmlinuz-trusty || goto selection

:dcore-xenial

  dhcp
  initrd http://tinycorelinux.net/dCore/x86/release/dCore-xenial/dCore-xenial.gz
  imgfetch http://tinycorelinux.net/dCore/x86/release/dCore-xenial/vmlinuz-xenial
  boot vmlinuz-xenial || goto selection

:arch

  dhcp

# archlinux uses imgtrust enforcing in new ipxe setup and this is a bit of a hassle to setup in ipxe
# instead just chainload their loader with necessary settings

# imgfetch http://www.archlinux.org/releng/netboot/archlinux.ipxe
#  boot archlinux.ipxe

  imgfetch https://www.archlinux.org/static/netboot/ipxe.8da38b4a9310.pxe
  boot ipxe.8da38b4a9310.pxe

